plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application
    id 'application'

    // Apply yWorks coding style with editorconfig
    id 'org.ec4j.editorconfig' version '0.0.3'
}

allprojects {
    apply plugin: 'org.ec4j.editorconfig'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lgpl')
    implementation 'com.google.guava:guava:28.1-android'
    implementation 'org.ow2.asm:asm:7.2'
    implementation 'org.apache.ant:ant:1.10.7'
    testImplementation 'junit:junit:4.13-beta-3'
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

application {
    mainClassName = 'com.yworks.yguard.YGuardLogParser'
}

editorconfig {
    includes = ['src/**', 'build.gradle']
    excludes = ['**/*.properties', '**/*.bits', '**/resources', '**/dist']
}

jar {
  manifest {
    attributes(
      'Class-Path': String.join(" ", "lgpl.jar", "asm-7.2.jar", "guava-28.1-android.jar")
    )
  }
}

def VERSION = String.format("%s.%s", project.findProperty('VERSION_MAJOR'), project.findProperty('VERSION_MINOR'))

distributions {
  main {
    baseName = String.format("yguard-%s", VERSION)
    contents {
      from file("$projectDir/LICENSE")
      into("docs") {
        from "$projectDir/docs"
        filter {
          line -> line
                  .replaceAll('@VERSION@', VERSION)
                  .replaceAll('@VERSION_MAJOR@', project.findProperty('VERSION_MAJOR'))
        }
      }
    }
  }
}

check.dependsOn editorconfigCheck
