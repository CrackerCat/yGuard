plugins {
    // Apply the java and library plugin to add support for Java
    id 'java'
    id 'java-library-distribution'

    // Apply yWorks coding style with editorconfig
    id 'org.ec4j.editorconfig' version '0.0.3'
}

allprojects {
    apply plugin: 'org.ec4j.editorconfig'
}

repositories {
    mavenCentral()
}

configurations {
    dependents.extendsFrom implementation
}

dependencies {
    implementation project(':retroguard')
    implementation project(':annotation')
    implementation 'com.google.guava:guava:28.1-android'
    implementation 'org.ow2.asm:asm:7.2'
    implementation 'org.apache.ant:ant:1.10.7'
    testImplementation 'junit:junit:4.13-beta-3'
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

def DEPENDANT_JARS = ["retroguard.jar", "asm-7.2.jar", "guava-28.1-android.jar", "failureaccess-1.0.1.jar"]

jar {
  manifest {
    attributes(
      'Class-Path': String.join(" ", *DEPENDANT_JARS),
      'Main-Class': 'com.yworks.yguard.YGuardLogParser'
    )
  }
}

def VERSION = String.format("%s.%s", project.findProperty('VERSION_MAJOR'), project.findProperty('VERSION_MINOR'))

distributions {
  bundle {
    baseName = String.format("yguard-%s", VERSION)
    contents {
      from file("$projectDir/LICENSE")
      into("lib") {
        from configurations.dependents.filter { it.name in DEPENDANT_JARS.plus('ObfuscationAnnotation.jar') }
        from jar
      }
      into("docs") {
        from "$projectDir/docs"
        filter {
          line -> line
                  .replaceAll('@VERSION@', VERSION)
                  .replaceAll('@VERSION_MAJOR@', project.findProperty('VERSION_MAJOR'))
        }
      }
    }
  }
}

editorconfig {
  includes = ['src/**', 'build.gradle']
  excludes = ['**/*.properties', '**/*.bits', '**/resources', '**/dist']
}

check.dependsOn editorconfigCheck
