plugins {
  // Apply the java and library plugin to add support for Java
  id 'application'
}

application {
  mainClassName = 'com.yworks.example.HelloWorld'
}

jar {
  manifest {
    attributes(
      'Main-Class': application.mainClassName
    )
  }
}

task copylibs(type: Copy) {
  def zipFile = file("../../build/distributions/yguard-${VERSION}.zip")
  def outputDir = file("${buildDir}/lib")

  from zipTree(zipFile)
  into outputDir
}

task yguard {
  dependsOn jar, copylibs
  group 'yGuard'
  description 'Obfuscates and shrinks the java archive.'

  doLast {
    ant.taskdef(
            name: 'yguard',
            classname: 'com.yworks.yguard.YGuardTask',
            classpath: "${buildDir}/lib/yguard-${VERSION}/lib/yguard.jar"
    )

    def archivePath = jar.archiveFile.get().asFile.path
    ant.yguard {
      inoutpair(in: archivePath, out: archivePath.replace(".jar", "_obf.jar"))
      shrink(logfile: "${buildDir}/yshrink.log.xml") {
        keep {
          method(name: "void main(java.lang.String[])", "class": application.mainClassName)
        }
      }
      rename(mainclass: application.mainClassName, logfile: "${buildDir}/yguard.log.xml") {
        property(name: "error-checking", value: "pedantic")
      }
    }
  }
}
